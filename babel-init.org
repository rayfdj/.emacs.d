* Babelized Emacs User Initialization File
This is my portable Emacs configuration, to make it easy for me to have the same Emacs across machines, VMs, etc.

** Initial Package Setup
Just for two things:
- Adds MELPA to package-archives. Pretty much all the useful extra packages are there.
#+BEGIN_SRC emacs-lisp
(require 'package)
(setq package-archives '(("gnu" . "http://elpa.gnu.org/packages/")
                         ("melpa" . "https://melpa.org/packages/")))
(package-initialize)
#+END_SRC

- Install use-package if it's not already installed. Note the call to package-refresh-contents to make sure this works with freshly installed Emacs too.
#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
(require 'use-package)
(setq use-package-always-ensure t)
#+END_SRC

** Common Configuration
Common configuration across all tasks.

*** Keys Re-mapping
To allow entering chars from non-English languages, map Meta to Command, and free up Option.
#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (setq mac-command-modifier 'meta
        mac-option-modifier 'none))
#+END_SRC

I really just want the equivalent of Ctrl+Tab and Ctrl+Shift+Tab. This one only needs you to shift your pinkies a bit.
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-'") #'other-window)
(global-set-key (kbd "C-;") #'prev-window)
(defun prev-window ()
  (interactive)
  (other-window -1))
#+END_SRC

*** Separate Customize Settings
The settings written by Customize aren't meant to be edited manually, so it's better to separate them into another file. Also, create the custom.el file if it doesn't already exist.
#+BEGIN_SRC emacs-lisp
(defconst custom-file-path (expand-file-name "custom.el" user-emacs-directory))
(unless (file-exists-p custom-file-path)
  (write-region "" nil custom-file-path))
(setq custom-file custom-file-path)
(load custom-file)
#+END_SRC

*** Miscellaneous
Start in home directory.
#+BEGIN_SRC emacs-lisp
(setq default-directory "~/")
#+END_SRC

Yes, when I quit, I do want to kill processes. No need to ask me.
#+BEGIN_SRC emacs-lisp
(setq confirm-kill-processes nil)
#+END_SRC

Put backup and autosave files in the tmp dir
#+BEGIN_SRC emacs-lisp
;; store all backup and autosave files in the tmp dir
(setq backup-directory-alist
      `((".*" . ,temporary-file-directory)))
(setq auto-save-file-name-transforms
      `((".*" ,temporary-file-directory t)))
#+END_SRC

*** UI Settings
The [Doom Themes](https://github.com/hlissner/emacs-doom-themes) are nice. I like the Nord Light theme.
#+BEGIN_SRC emacs-lisp
(use-package doom-themes
  :config
  ;; Global settings (defaults)
  (setq doom-themes-enable-bold t    ; if nil, bold is universally disabled
        doom-themes-enable-italic t) ; if nil, italics is universally disabled
  (load-theme 'doom-nord-light t)

  ;; Enable flashing mode-line on errors
  (doom-themes-visual-bell-config)
    
  ;; Corrects (and improves) org-mode's native fontification.
  (doom-themes-org-config))
#+END_SRC

Disable the startup screen.
#+BEGIN_SRC emacs-lisp
(setq inhibit-startup-message t)
#+END_SRC

Disable the menu, scrollbar, and toolbar. They reset initial frame's size so have to be done first.
#+BEGIN_SRC emacs-lisp
(menu-bar-mode -1)
(scroll-bar-mode -1)
(tool-bar-mode -1)
#+END_SRC

Set initial frame to handle 160x48 characters, and center it on the screen.
#+BEGIN_SRC emacs-lisp
;; Set initial frame size and position
(defun set-initial-frame (max-chars-per-line max-no-of-lines)
  (let* ((frame-width (* (window-font-width) max-chars-per-line))
	 (frame-height (* (window-font-height) max-no-of-lines))
	 (frame-left (truncate (/ (- (display-pixel-width) frame-width) 2)))
	 (frame-top (truncate (/ (- (display-pixel-height) frame-height) 2))))
    (set-frame-position (selected-frame) frame-left frame-top)
    (set-frame-size (selected-frame) frame-width frame-height t)))
(when (display-graphic-p)
  (setq frame-resize-pixelwise t)
  (set-initial-frame 160 51)) ; +3 lines for the title bar/mode line/minibuffer.
#+END_SRC

Line and column modes.
#+BEGIN_SRC emacs-lisp
(column-number-mode 1)
(global-hl-line-mode t)
(global-linum-mode)
#+END_SRC

** Org-mode configurations
Org and agenda settings
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/Documents/org-roam")
(setq org-agenda-files (directory-files-recursively org-directory "\\.org$"))
#+END_SRC

Org mobile settings
#+BEGIN_SRC emacs-lisp
(setq org-mobile-directory "~/Library/Mobile Documents/iCloud~com~mobileorg~mobileorg/Documents")
(setq org-mobile-inbox-for-pull (expand-file-name "from-mobile.org" org-directory))
#+END_SRC

Customize TODO states.
#+BEGIN_SRC emacs-lisp
(setq org-log-done 'time)
(setq org-todo-keywords
      '((sequence "TODO(t)" "ONGOING(o@/!)" "|" "DONE(d!)" "DELEGATED(e@)" "CANCELED(c@)")))
#+END_SRC

Don't fold org files. Always open expanded.
#+BEGIN_SRC emacs-lisp
(setq org-startup-folded nil)
#+END_SRC

** Packages
Packages I want to install with every fresh Emacs.
#+BEGIN_SRC emacs-lisp
(use-package exec-path-from-shell
  :if (memq window-system '(mac ns))
  :config
  (exec-path-from-shell-initialize))

(use-package magit)

(use-package company
  :config
  (add-hook 'after-init-hook 'global-company-mode))

(use-package counsel
  :after ivy
  :config (counsel-mode))

(use-package ivy
  :defer 0.1
  :diminish
  :bind (("C-c C-r" . ivy-resume)
         ("C-x B" . ivy-switch-buffer-other-window))
  :custom
  (ivy-count-format "(%d/%d) ")
  (ivy-use-virtual-buffers t)
  :config (ivy-mode))

(use-package prescient)

(use-package ivy-prescient
  :after counsel ; see https://github.com/raxod502/prescient.el#usage
  :config (ivy-prescient-mode))

(use-package swiper
  :after ivy
  :bind (("C-s" . swiper)
         ("C-r" . swiper)))

(use-package multiple-cursors
  :bind (("C-c m c" . mc/edit-lines)))

(use-package org-roam
  :hook
  (after-init . org-roam-mode)
  :custom
  (org-roam-directory org-directory)
  (org-roam-dailies-capture-templates
    '(("d" "default" entry (function org-roam-capture--get-point)
     "* %?"
     :file-name "daily/%<%Y-%m-%d>"
     :head "#+title: %<%B %e, %Y>\n#+roam_tags: \n\n* "))))

(use-package racket-mode)

(use-package projectile
  :init
  (projectile-mode +1)
  :config
  (setq projectile-project-search-path '("~/projectiles/")))
#+END_SRC
